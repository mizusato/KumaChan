do DialogDemoWindow.show;

do DialogDemoWindow.closed.listen (&(_)(exit 0));

do (merge [@open, @open-multiple, @open-directory, @save])
    | scan '' (&(text,line)("##"..\n text line))
    | concat-map (&(text)(LogBox.set-plain-text text))
    | forever;

private const @open: NoExcept*[String]
    ButtonOpen.clicked
    | concat-map ( &(_)
        qt-dialog-open {
            parent: Just DialogDemoWindow,
            title:  'Open',
            cwd:    OS::Cwd,
            filter: 'Text files (*.txt);;Markdown files (*.md)'
        }
    )
    | map* ( &(path?)
        switch path?:
        case Just path:
            "open: #" (str path),
        case N/A:
            'open: (cancelled)',
        end
    );

private const @open-multiple: NoExcept*[String]
    ButtonOpenMultiple.clicked
    | concat-map ( &(_)
        qt-dialog-open* {
            parent: Just DialogDemoWindow,
            title:  'Open Multiple',
            cwd:    OS::Cwd,
            filter: 'Text files (*.txt)'
        }
    )
    | map* ( &(path-list)
        if $((path-list.length) > 0):
            "open multiple: #" (
                path-list
                | iterate
                | map (&(path)(str path))
                | map "'#'"
                | join ' '),
        else:
            'open multiple: (cancelled)'
    );

private const @open-directory: NoExcept*[String]
    ButtonOpenDirectory.clicked
    | concat-map ( &(_)
        qt-dialog-open-dir {
            parent: Just DialogDemoWindow,
            title:  'Open Directory',
            cwd:    OS::Cwd
        }
    )
    | map* ( &(path?)
        switch path?:
        case Just path:
            "open directory: #" (str path),
        case N/A:
            'open directory: (cancelled)',
        end
    );

private const @save: NoExcept*[String]
    ButtonSave.clicked
    | concat-map ( &(_)
        qt-dialog-save {
            parent: Just DialogDemoWindow,
            title:  'Save',
            cwd:    OS::Cwd,
            filter: 'Text files (*.txt)'
        }
    )
    | map* ( &(path?)
        switch path?:
        case Just path:
            "save: #" (str path),
        case N/A:
            'save: (cancelled)',
        end
    );
