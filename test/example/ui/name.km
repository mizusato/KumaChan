type Name {
    first: String,
    last:  String
};

do ui-init 'Name' DOM;

do UI::ExitWhenWindowClosed;

private function cpt-label: ( &(String) NoExcept*[UI::Node] )
    ( &(text) (yield (dom-node 'span' text)) );

private function cpt-button: ( &(String,UI::EventSink) NoExcept*[UI::Node] )
    ( &(title,sink) (yield (dom-node 'button' {
        content: (dom-text title),
        events:  (dom-event 'click' sink)
    })) );

private function cpt-text-input: ( &(Reactive[String]) NoExcept*[UI::Node] )
    ( &(@value)
        ~ map* &(value) := watch @value,
        dom-node 'input' (dom-styles [])
            (dom-attrs [('value',value)])
            (dom-event 'input'
                (dom-event-sink @value (&(ev)(ev.get-string 'webuiValue'))))
            (dom-children [])
    );

private const DOM: NoExcept*[UI::Node]
    ~ with-reactive* &(name) := Name { first: '', last: '' },
    let {first,last} := name,
    dom-dynamic 'div' [('padding','0.5rem 0.5rem')] [
        dom-dynamic 'div' [
            cpt-label 'First Name:',
            cpt-text-input first,
            cpt-button 'clear' (dom-callback (& _ (first.update (& _ ''))))
        ],
        dom-dynamic 'div' [
            cpt-label 'Last Name:',
            cpt-text-input last,
            cpt-button 'clear' (dom-callback (& _ (last.update (& _ ''))))
        ],
        dom-dynamic 'p' [
            cpt-label 'Full Name:',
            (~ map* &{first,last} := watch name,
                (dom-node 'span'
                    [('color','red'),('border','1px solid red'),('padding','0px 5px')]
                    ("# #" first last)))
        ]
    ];
