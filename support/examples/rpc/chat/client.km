import service from './service';

export function consume-chat:
    &() => service::Consumer
    &() => &(chat) =>
        { println 'connected' }
            . { then chat.{ receive-messages () } }
            . { concat-map &(msg) =>
                { println { "#: #" (msg.from, msg.content.text) } } }
            . { wait-complete }
            . { crash-on-error }
            . { with
                let read-and-send :=
                    | text := await scanln,
                    | await chat.{ say { service::MessageContent { text } } },
                    { yield () },
                read-and-send.{forever}.{crash-on-error} };

do
    let login-and-chat :=
        | await { print '(login) nickname: ' },
        | nickname := await scanln,
        { access {
            service: service::Identifier,
            backend: { ClientCleartext { network: 'tcp', addr: '127.0.0.1:8002' } },
            options: {},
            argument: { service::Argument { service::Login { nickname } } },
            consumer: consume-chat
        } },
    login-and-chat
        . { catch-retry &(err) =>
            let kind := (err get-data service::ERR_KIND),
            if (kind = service::ERR_NAME_IN_USE):
                | await { println '*** This name is in use. Try another.' }
                    .{ crash-on-error },
                { yield Yes },
            else:
                { yield No }
        }
        . { crash-on-error };
