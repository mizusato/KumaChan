import assets from './assets';

type Todo {
    id:       String,
    content:  String,
    finished: Bool
};


do ui-init 'Reactive List' Root;

do ui-inject-css [assets::style-css];

do ui-exit-when-window-closed;

private const Root: UI::Component
    let center := styled ui-container ['center'],
    let row := styled ui-container ['row'],
    ~ with-reactive* &(list) := new-list:[Todo] [] ( &{id} (ListKey id) ),
    center [
        ~ with-reactive* &(buffer) := '',
        row [
            buffer.ui-text-input,
            buffer.watch.switch-map( &(content)
                ui-button 'Add' (
                    proc-gid.then ( &(id) list.update( &(list)
                        list.append (id, (Todo { id, content, finished: No })
                    ))).then $(buffer <- '')
                ))
        ],
        ~ use &(id, index, item) := ui-list list,
        let { content, finished } := item,
        let is-first := (index.map* (&(index) $(index = 0))),
        let is-last :=
            (~ map* &(index,length) := combine-latest! index (list.watch.map*(&(list)(list.length))),
            $(index = $(length - 1))),
        row [
            ui-checkbox finished,
            ui-text-input content,
            row [
                ~ switch-map &(is-first) := is-first,
                if is-first: ui-empty, else:
                    ui-button 'Up' (list.update(&(list)(list.move-up id))),
                ~ switch-map &(is-last) := is-last,
                if is-last: ui-empty, else:
                    ui-button 'Down' (list.update(&(list)(list.move-down id))),
                ui-button 'Remove' (list.update(&(list)(list.delete id)))
            ]
        ]
    ];
