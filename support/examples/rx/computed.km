import window from '../ui/web/window';

do { window::mount Root };

function RealInput:
    &(Reactive[Maybe[Real]]) => ui::Component
    &(value) =>
        & buf := use { State '' },
        & use { Effect (buf connect &(s) => (value <- { parse-float s })) },
        { Input buf };

const Root: ui::Component :=
    let container := { StyledContainer ['container'] },
    let row := { StyledContainer ['row'] },
    & price := use { State { Just 0.0 } },
    & quantity := use { State { Just 0.0 } },
    let total :=
        & (price?, quantity?) := Computed (price.{watch}, quantity.{watch}),
        & price := maybe? price?,
        & quantity := maybe? quantity?,
        { Just (price * quantity) },
    { container [
        { row [ { Label 'Price:' }, { RealInput price } ] },
        { row [ { Label 'Quantity:' }, { RealInput quantity } ] },
        { Label
            & total? := map total,
            switch total?:
            case Just total:
                { "Total = #" total.{str} },
            case Null:
                'invalid input',
            end
        }
    ] };
