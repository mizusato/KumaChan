import assets from './assets';

type Todo {
    content:  String,
    finished: Bool
};


do ui-init 'Reactive List' Root;

do ui-inject-css [assets::style-css];

do ui-exit-when-window-closed;

const Root: UI::Component
    let center := styled ui-container ['center'],
    let row := styled ui-container ['row'],
    let button-small := styled ui-button ['min-width-3'],
    ~ use &(list) := ui-state (empty-list:[Todo]),
    let add-item-from: (&(Reactive[String])(Action)) := ( &(buffer)
        ~ await &(content) := buffer.read,
        ~ await &(id) := gen-sequential-id,
        ~ await list.append (id, { content, finished: No }),
        buffer.write('')
    ),
    let insert-item-after: (&(ListKey)(Action)) := ( &(pivot)
        ~ await &(id) := gen-sequential-id,
        list.insert {after:pivot} (id, { content: '', finished: No })
    ),
    center [
        ~ use &(buffer) := ui-state '',
        row [
            ui-text-input buffer,
            ui-button 'Add' (add-item-from buffer)
        ],
        ~ use &(id, index, item) := ui-list list,
        let { content, finished } := item,
        row [
            ui-checkbox finished,
            ui-text-input content,
            row [
                button-small '↑' (list.move-up id)
                    | with (ui-enabled (not $(index is-first-in list))),
                button-small '↓' (list.move-down id)
                    | with (ui-enabled (not $(index is-last-in list))),
                button-small '+' (insert-item-after id),
                button-small '×' (list.delete id)
            ]
        ]
    ];
