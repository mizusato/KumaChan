// QWidget
type Widget protected Object;

public function show: (&(Widget)(NoExcept[-]))
    native 'qt-show';

public function move-to-screen-center: (&(Widget)(NoExcept[-]))
    native 'qt-move-to-screen-center';


// QMainWindow
type MainWindow protected Widget;


// QLabel
type Label protected Widget;

public function set-text: ( &(Label,String) NoExcept[-] )
    (&(label,text)(set-property label 'text' 'String' text));


// QLineEdit
type LineEdit protected Widget;

private function get-text: ( &(Static[LineEdit]) String )
    (&(line)(get-property line 'text' 'String'));

public function text: ( &(LineEdit) Signal[String] )
    (&(line)(signal line 'textEdited(const QString&)' get-text));

public function enter: ( &(LineEdit) Signal[-] )
    (&(line)(signal line 'returnPressed()'));

public function set-text: ( &(LineEdit,String) NoExcept[-] )
    (&(line,text)(set-property line 'text' 'String' text));


// QPlainTextEdit
type PlainTextEdit protected Widget;

private function get-plain-text: ( &(Static[PlainTextEdit]) String )
    (&(plain)(get-property plain 'plainText' 'String'));

// no user edit signal
public function plain-text: ( &(PlainTextEdit) Signal[String] )
    (&(plain)(signal plain 'textChanged()' get-plain-text));

public function set-plain-text: ( &(PlainTextEdit,String) NoExcept[-] )
    (&(plain,text)(set-property plain 'plainText' 'String' text));


// QPushButton
type PushButton protected Widget;

public function clicked: ( &(PushButton) Signal[-] )
    (&(button)(signal button 'clicked()'));


// QCheckBox
type CheckBox protected Widget;

private function get-checked: ( &(Static[CheckBox]) Bool )
    (&(checkbox)(get-property checkbox 'checked' 'Bool'));

public function checked: ( &(CheckBox) Signal[Bool] )
    (&(checkbox)(signal checkbox 'clicked(bool)' get-checked));

public function set-checked: ( &(CheckBox,Bool) NoExcept[-] )
    (&(checkbox,checked)(set-property checkbox 'checked' 'Bool' checked));


// QComboBox
type ComboBox protected Widget;

private function get-current-text: ( &(Static[ComboBox]) String )
    (&(combo)(get-property combo 'currentText' 'String'));

public function current-text: ( &(ComboBox) Signal[String] )
    (&(combo)(signal combo 'currentTextChanged(const QString&)' get-current-text));

public function set-current-text: ( &(ComboBox,String) NoExcept[-] )
    (&(combo,text)(set-property combo 'currentText' 'String' text));


// QListWidget
type ListWidget protected Widget;
type ListWidgetItem native;

public function set-items: ( &(ListWidget,Array[ListWidgetItem],Maybe[String]) NoExcept[-] )
    native 'qt-list-widget-set-items';

public function list-widget-item: ( &(String,String) ListWidgetItem )
    native 'qt-list-widget-item';

public function list-widget-item: ( &(String,Image::PNG,String) ListWidgetItem )
    native 'qt-list-widget-item-with-icon-png';

private function get-current-item: ( &(Static[ListWidget]) Maybe[String] )
    native 'qt-list-widget-get-current';

public function current-item: ( &(ListWidget) Signal[Maybe[String]] )
    (&(list)(signal list 'currentRowChanged(int)' get-current-item));
