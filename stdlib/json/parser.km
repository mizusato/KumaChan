type Parser[+T,+E]
    &(String) => Result[(T,String),(E,String)];

function make-lazy:[T,E]
    &(&() => Parser[T,E]) => Parser[T,E]
    &(thunk) => &(input) =>
        let parse := { thunk () },
        { parse input };

function output:[T,E]
    &(T) => Parser[T,E]
    &(value) => &(input) => { OK (value, input) };

function throw:[T,E]
    &(E) => Parser[T,E]
    &(err) => &(input) => { NG (err, input) };

function consume:[E]
    &(String, E) => Parser[unit,E]
    &(target, err) => &(input) =>
        if input.[String].{ has-prefix target }:
            let input := input.{substr-view (target.{length}, input.{length})},
            { OK ((), input) },
        else:
            { NG (err, input) };

function with-ignored:[T,E]
    &(Parser[T,E], Parser[unit,E]) => Parser[T,E]
    &(parse-t, parse-blank) =>
        let parse-blanks: rec
            &(String) => String :=
            &(input) =>
                switch { parse-blank input }:
                case OK (_, input):
                    { parse-blanks input },
                case NG _:
                    input,
                end,
        &(input) =>
            { parse-t { parse-blanks input } }
                . { map &(t, input) => (t, { parse-blanks input }) };

function map:[A,B,E]
    &(Parser[A,E], &(A) => B) => Parser[B,E]
    &(parse-a, a->b) => &(input) =>
        switch { parse-a input }:
        case OK (a, input):
            { OK ({ a->b a }, input) },
        case NG err:
            { NG err },
        end;

function apply:[T,R,E]
    &(Parser[T,E], &(T) => Parser[R,E]) => Parser[R,E]
    &(parse, k) => &(input) =>
        switch { parse input }:
        case OK (value, input):
            let parse-next := { k(value) },
            { parse-next input },
        case NG err:
            { NG err },
        end;

function choose:[T,E]
    &(Array[Parser[T,E]]) => Parser[T,E]
    &(parsers) =>
        let first := (parsers at! 0),
        let rest := (parsers slice-view (1, parsers.{length})),
        (rest reduce (first, &(parse-previous, parse-this) => &(input) =>
            switch { parse-previous input }:
            case OK parsed:
                { OK parsed },
            case NG _:
                { parse-this input },
            end
        ));

function repeat:[T,E]
    &(Parser[T,E]) => Parser[Array[T],E]
    &(item) => &(input) =>
        let proceed: rec
            &(String, Seq[T]) => Result[(Seq[T],String),(E,String)] :=
            &(input, seq) =>
                switch { item input }:
                case OK (value, input):
                    { proceed (input, (value cons seq)) },
                case NG _:
                    { OK (seq, input) },
                end,
        { proceed (input, nil) }
            . { map &(seq, input) => (seq.{collect}.{reverse}, input) };

function repeat:[T,E]
    & { item: Parser[T,E], sep: Parser[unit,E] } => Parser[Array[T],E]
    & { item, sep } => &(input) =>
        let proceed: rec
            &(String, Seq[T]) => Result[(Seq[T],String),(E,String)] :=
            &(input, seq) =>
                switch { item input }:
                case OK (value, input):
                    let seq := (value cons seq),
                    switch { sep input }:
                    case OK (_, input):
                        { proceed (input, seq) },
                    case NG _:
                        { OK (seq, input) },
                    end,
                case NG (err, input):
                    if seq.{is-nil}:
                        { OK (nil, input) },
                    else:
                        { NG (err, input) },
                end,
        { proceed (input, nil) }
            . { map &(seq, input) => (seq.{collect}.{reverse}, input) };
