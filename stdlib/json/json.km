type Value enum {
    type Object  Map[String,Value];
    type Array   Array[Value];
    type Boolean Bool;
    type Number  Float;
    type String  String;
    type Null;
};

// TODO: implement json parser using parser combinators

export function json-stringify:
    &(Self::Value) => String
    &(v) =>
        switch v:
        case Self::Object object:
            object
                . { iterate }
                . { map &(key,value) =>
                    { "#:#" ({quote key}, {json-stringify value}) } }
                . { join ',' }
                . { "{#}" },
        case Self::Array array:
            array
                . { iterate }
                . { map json-stringify }
                . { join ',' }
                . { "[#]" },
        case Self::Boolean boolean:
            switch boolean:
                case Yes: 'true',
                case No:  'false',
            end,
        case Self::Number number:
            { str number },
        case Self::String string:
            { quote string },
        case Self::Null:
            'null',
        end;
